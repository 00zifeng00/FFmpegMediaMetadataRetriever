apply plugin: 'com.android.library'

android {
    compileSdkVersion 22
    buildToolsVersion "21.1.2"

    def mkfile = "NDK_APPLICATION_MK=Application.mk"
    //def mkfile = "NDK_APPLICATION_MK=ARMApplication.mk"
    //def mkfile = "NDK_APPLICATION_MK=ARMv7Application.mk"
    //def mkfile = "NDK_APPLICATION_MK=x86Application.mk"
    //def mkfile = "NDK_APPLICATION_MK=MIPSApplication.mk"


    defaultConfig {
        minSdkVersion 7
        targetSdkVersion 22
        versionCode 2
        versionName "1.0.1"
    }
    buildTypes {
        release {
            //minifyEnabled false
            //proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets.main {
        jni.srcDirs = [] // This prevents the auto generation of Android.mk
        jniLibs.srcDir 'src/main/libs' // This is not necessary unless you have precompiled libraries in your project.
    }

    task buildFFmpeg(type: Exec, description: 'Compile FFmpeg source via NDK') {
        def dir = file('src/main/scripts/build-ffmpeg.sh').absolutePath
        commandLine "$dir"
    }

    task buildNative(type: Exec, description: 'Compile JNI source via NDK') {
        def ndkDir = android.ndkDirectory
        print mkfile
        commandLine "$ndkDir/ndk-build",
                mkfile,
                '-C', file('src/main/jni').absolutePath, // Change src/main/jni the relative path to your jni source
                '-j', Runtime.runtime.availableProcessors(),
                'all',
                'NDK_DEBUG=1'
    }

    task cleanNative(type: Exec, description: 'Clean JNI object files') {
        def ndkDir = android.ndkDirectory
        commandLine "$ndkDir/ndk-build",
                '-C', file('src/main/jni').absolutePath, // Change src/main/jni the relative path to your jni source
                'clean'
    }

    clean.dependsOn 'cleanNative'
    buildNative.dependsOn 'buildFFmpeg'

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn buildNative
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

apply from: 'maven-push.gradle'